{
  inputs,
  lib,
  flake-parts-lib,
  ...
}:
let
  inherit (lib) types mkOption;
  inherit (flake-parts-lib) mkPerSystemOption;
in
{
  imports = [ inputs.flake-parts-website.flakeModules.empty-site ];

  options.perSystem = mkPerSystemOption (
    { pkgs, ... }:
    {
      options.pythonWithMkdocs = mkOption {
        description = ''
          python instance with suitable mkdocs plugins included
        '';
        type = types.package;
        default = pkgs.python3.withPackages (
          ps: with ps; [
            mkdocs
            mkdocs-material
          ]
        );
        internal = true;
      };
    }
  );

  config.perSystem =
    {
      pkgs,
      config,
      self',
      ...
    }:
    {
      render.inputs.self = {
        baseUrl = "https://github.com/fleek-platform/nix2vast/blob/main";
        title = "nix2vast";
        sourceName = "nix2vast";
        intro = ''
          `nixos` containers for cost-effective and capable gpu compute. [vast.ai](https://vast.ai) is the first target, there will be many more.

          // init // what this is

          a nix-based container runtime that makes distributed gpu compute actually work. reproducible environments, tailscale networking, `CUDA` 12.8. everything you need to turn random gpus into a coherent compute cluster.

          `vast.ai` is just the beginning. any gpu, anywhere, real init system, same environment.
        '';
        separateEval = true;
        extraInputs = inputs;
      };

      packages.docs = pkgs.stdenvNoCC.mkDerivation {
        name = "nix2vast-docs";

        src = ../.;

        nativeBuildInputs = [
          config.pythonWithMkdocs
          pkgs.mkdocs
        ];

        preBuild = ''
          cp -f "${self'.packages.generated-docs-self}/options.md" docs/options.md

          # Fixup markdown titles generated by nixos options doc
          sed -i 's/^## \(.*\) {#.*}$/## `\1`/' docs/options.md

          # Remove backslash escaping from option names within backticks
          sed -i '/^## `.*`$/ { :a; s/`\([^`]*\)\\\([^`]*\)`/`\1\2`/g; ta }' docs/options.md
        '';

        buildPhase = ''
          runHook preBuild

          mkdocs build

          runHook postBuild
        '';

        installPhase = ''
          runHook preInstall

          mkdir -p "$out/share/nix2vast/site"

          cp -r site/. "$out/share/nix2vast/site"

          runHook postInstall
        '';
      };
    };
}
